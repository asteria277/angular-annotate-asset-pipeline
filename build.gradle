buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:0.6'
    }
}

apply plugin: 'com.moowork.node'

node {
    version = '0.11.10'
    distBaseUrl = 'http://nodejs.org/dist'
    download = true
    workDir = file("${project.buildDir}/nodejs")
}

project.ext {
    tempFile = file('ngannotate.js')
    destination = file('src/groovy/com/craigburke/angular/')
    npmDependencies = ['ng-annotate', 'browserify']
}

task clean(type: Delete) {
    tempFile
}

task installDependencies(type: NpmTask) {
    args = ['install'] + npmDependencies + ['--save-dev']
}

task bundleNgAnnotate(type: NodeTask, dependsOn: ['installDependencies']) {
	script = file('node_modules/browserify/bin/cmd.js')
	args = ['node_modules/ng-annotate/build/es5/ng-annotate-main.js', '-o', tempFile.path]
}

task installNgAnnotate(dependsOn: 'bundleNgAnnotate') << {
    tempFile.text = "var ngAnnotate;${tempFile.text}"

    copy {
        from tempFile
        into destination
        filter { String line ->
            line.replace('module.exports = function ngAnnotate', 'ngAnnotate = function ngAnnotate')
        }
    }

    delete { tempFile }
}